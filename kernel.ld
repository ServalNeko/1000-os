/* カーネルのエントエリーポイントはboot関数 */
ENTRY(boot)

SECTIONS {
    /* ベースアドレス */
    . = 0x80200000;

    /* コード領域 */
    .text :{
        KEEP(*(.text.boot));
        *(.text .text.*);
    }
    /* ALIGN(4)は「4バイト境界になるようにアドレスを調整する」 */
    /* 定数データ領域 読取り専用 */
    .rodata : ALIGN(4) {
        *(.rodata .rodata.*);
    }

    /* 読み書き可能データ領域 */
    .data : ALIGN(4) {
        *(.data .data.*);
    }

    /* 読み書き可能データ領域 .dataと違い、初期値がゼロの変数を配置する*/
    /*
    「__bss = .」のような記述は、__bssというシンボルに現在のアドレスを割り当てるという意味になります。 
    「シンボル」 は関数や静的変数を表すもので、C言語では extern char シンボル名 で定義したシンボルを参照できます。
    */
    .bss : ALIGN(4) {
        __bss = .;
        *(.bss .bss.* .sbss .sbss.*);
        __bss_end = .;
    }

    . = ALIGN(4);
    . += 128 * 1024; /* 128KB */
    __stack_top = .;

    /* 割当可能なメモリサイズは64MB アラインメントは4KB */
    . = ALIGN(4096);
    __free_ram = .;
    . += 64 * 1024 * 1024; /* 64MB */
    __free_ram_end = .;

}